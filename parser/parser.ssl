%  PT Parser Version 5.0
%
% Date:  30 Jun 1980
%        (Revised v5.0 - 10 Dec 2020)
%
% Author:  Alan Rosselet
%
% Abstract
%     This module parses the syntax token stream emitted by the scanner
%     and emits a postfix stream of tokens to the semantic analyzer
%     module.  The parser detects and repairs syntax errors so that the
%     token stream emitted to the semantic analyzer is syntactically
%     legal (except for the special case of declaration phase errors - 
%     see the comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order

        pChoose                 'choose'
        firstKeywordToken = pChoose
        pElseif                 'elseif'
        pOr                     'or'
        pAnd                    'and'
        pNot                    'not'
        pThen                   'then'
        pElse                   'else'
        pOf                     'of'
        pEnd                    'end'
        pFun                    'fun'
        pIs                     'is'
        pLike                   'like'
        pFile                   'file'
        pPkg                    'pkg'
        pPublic                 'public'
        pVar                    'var'
        pUsing                  'using'
        pVal                    'val'
        pWhen                   'when'
        pIf                     'if'
        pWhile                  'while'
        pRepeat                 'repeat'
        lastKeywordToken = pRepeat 


        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        pColonEquals            ':='
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '!='
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        pDotDot                 '..'

        pSlash                  '/'
        pPercent                '%'
        pHash                   '#'
        pBar                    '|'
        pBang                   '!'
        pDoubleBar              '||'
        pPlusEquals             '+='
        pMinusEquals            '-='
        pStarEquals             '*='
        pSlashEquals            '/='
        pPercentEquals          '%='
        pDoubleEquals           '=='
        pLeftBrace              '{'
        pRightBrace             '}'

        lastSyntaxToken = pRightBrace;

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral

        sProgram
        sParmBegin
        sParmEnd
        sConst
        sVar
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sWhileStmt
        sRepeatStmt
        sRepeatEnd
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sNewLine
        sEndOfFile

        sLike
        sPackage
        sPublic
        sConcatinate
        sRepeatString
        sSubstring
        sLength
        sInitialValue
        sCaseElse

        lastSemanticToken = sCaseElse;


rules

Program :
        'using'  .sProgram

        % program parameters, needs to be at least 1
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }

        .sParmEnd ';'
        .sBegin  @Block .sEnd ;

% this becomes the body for all declarations and statements
Block :
        {[
            | 'val':
                .sConst
                @NonPublicConstantDefinitions

            | 'var':
                .sVar
                @NonPublicVariableDeclarations

            | pIdentifier:
                @AssignmentOrCallStmt

            | 'pkg':
                @PkgStatement

            | 'if':
                @IfStmt
            
            | 'choose':
                @ChooseStmt

            | 'fun' :
                .sProcedure
                @NonPublicFunction

            | 'public' :
                % can have either val, var or fun
                [
                    | 'val' :
                        .sConst
                        @PublicConstantDefinition
                    | 'var' :
                        .sVar
                        @PublicVariableDeclaration
                    | 'fun' :
                        .sProcedure
                        @PublicFunctionDeclaration
                ]
                
            | 'repeat':
                | 'while':
                    @WhileStmt
                |*:
                    @RepeatStmt

            | *:
                >
        ]};

PublicVariableDeclaration :
        % Accept one variable declarations.
        pIdentifier  .sIdentifier
        .sPublic
        [
            | '=' :
                .sInitialValue
                @Expression .sExpnEnd
                ';'
            | * :
                @LikeClause
                ';'
        ];



PublicConstantDefinition :
        % Accept one or more named constant definitions
        pIdentifier  .sIdentifier
        .sPublic
        '=' @ConstantValue
        {[  
            | ',' :
                pIdentifier .sIdentifier
                '=' @ConstantValue 
            | ';' :
                >
        ]};

    
PublicFunctionDeclaration :
        % procedure name
        pIdentifier  .sIdentifier
        .sPublic
        
        @ProcedureHeading
        'is' .sBegin 
        @Block 
        'end' .sEnd ';'
    ;

NonPublicFunction :
        % procedure name
        pIdentifier  .sIdentifier
        
        @ProcedureHeading
        'is' .sBegin 
        @Block 
        'end' .sEnd ';'
    ;

NonPublicConstantDefinitions :
        % Accept one or more named constant definitions
        pIdentifier  .sIdentifier
        '=' @ConstantValue
        {[  
            | ',' :
                pIdentifier .sIdentifier
                '=' @ConstantValue 
            | ';' :
                >
        ]};

ConstantValue :
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pStringLiteral:
                .sStringLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

NonPublicVariableDeclarations :
        % Accept one variable declarations.
        pIdentifier  .sIdentifier
        [
            | '=' :
                .sInitialValue
                @Expression .sExpnEnd
                ';'
            | * :
                @LikeClause
                ';'
        ];

LikeClause :
    % Check for optinal array
    [
        | '[' :
            .sArray @UnsignedIntegerConstant ']'
        | * :
    ]
    ':'

    %check for optinoal file
    [
        | 'file' :
            .sFile
        | * :
    ]
    
    % match the type
    'like' .sLike @ConstantValue
    ;


ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
        [
            | '(':
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    @LikeClause
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:
        ]
        .sParmEnd;

 
%Statement :
%        [
%            | pIdentifier:
%                @AssignmentOrCallStmt
%            | 'begin':
%                @BeginStmt
%            | 'if':
%                @IfStmt
%            | 'while':
%                @WhileStmt
%            | 'choose':
%                @ChooseStmt
%            | 'repeat':
%                @RepeatStmt
%            | *:
%                .sNullStmt
%        ];


PkgStatement:
    .sPackage
    pIdentifier .sIdentifier
    .sBegin
        @Block
    .sEnd
    ;

AssignmentOrCallStmt :
        [
            | ':=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                @Expression
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
                ']'  ':='
                @Expression
                .sExpnEnd
            | *:
                @CallStmt
        ];

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statement rule
        % accept zero or more actual parameters
        [
            | '(':
                {
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:        % no actual parameters
        ]
        .sParmEnd;

BeginStmt :
        .sBegin
        {
            @Statement
            [
                | ';':
                | 'end':
                    .sEnd
                    >
            ]
        };

IfStmt :
        .sIfStmt
        @Expression
        .sExpnEnd
        'then'  .sThen
        .sBegin @Block .sEnd
        %this is missing the begin and ends
        {[
            | 'elseif':
                .sElse
                .sIfStmt
                @Expression
                .sExpnEnd
                'then'  .sThen
                .sBegin @Block .sEnd
            | *:
                >
        ]}
        [
            | 'else':
                .sElse
                .sBegin @Block .sEnd
            | *:
        ];

ChooseStmt :
        .sCaseStmt
        @Expression
        .sExpnEnd
        'of' 
        @CaseAlternative
        {[
            | 'end':
                .sEnd ';'
                >
                        
            | 'when':
                @CaseAlternative
        ]}
        .sCaseEnd
        [
            | 'else':
                .sCaseElse
                .sBegin @Block 
                'end' .sEnd ';'
            | *:
        ];

CaseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants

        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        .sLabelEnd
        'then' 
        .sBegin @Block .sEnd;

WhileStmt :
        .sWhileStmt
        @Expression
        .sExpnEnd
        .sBegin @Block 
        'end' .sEnd ';';

RepeatStmt :
        .sRepeatStmt
        {
            .sBegin @Block .sEnd
            [
                | 'while':
                    .sRepeatEnd
                    >
            ]
        }
        @Expression .sNot
        .sExpnEnd;

Expression :
        @SimpleExpression
        [
            | '==':
                @SimpleExpression  .sEq
            | '!=':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | *:
                >
        ]};

Term :
        @Factor
        {[
            | '*':
                @Factor  .sMultiply
            | '/':
                @Factor  .sDivide
            | '%':
                @Factor  .sModulus
            | 'and':
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Factor :
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | 'not':
                @Factor
                .sNot
            | pStringLiteral:
                .sStringLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
