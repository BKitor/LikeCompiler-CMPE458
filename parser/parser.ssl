%  PT Parser Version 5.0
%
% Date:  30 Jun 1980
%        (Revised v5.0 - 10 Dec 2020)
%
% Author:  Alan Rosselet
%
% Abstract
%     This module parses the syntax token stream emitted by the scanner
%     and emits a postfix stream of tokens to the semantic analyzer
%     module.  The parser detects and repairs syntax errors so that the
%     token stream emitted to the semantic analyzer is syntactically
%     legal (except for the special case of declaration phase errors - 
%     see the comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order

        pChoose                 'choose'
        firstKeywordToken = pChoose
        pElseif                 'elseif'
        pOr                     'or'
        pAnd                    'and'
        pNot                    'not'
        pThen                   'then'
        pElse                   'else'
        pOf                     'of'
        pEnd                    'end'
        pFun                    'fun'
        pIs                     'is'
        pLike                   'like'
        pFile                   'file'
        pPkg                    'pkg'
        pPublic                 'public'
        pVar                    'var'
        pUsing                  'using'
        pVal                    'val'
        pWhen                   'when'
        pIf                     'if'
        pWhile                  'while'
        pRepeat                 'repeat'
        lastKeywordToken = pRepeat 


        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '!='
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'

        pSlash                  '/'
        pPercent                '%'
        pHash                   '#'
        pBar                    '|'
        pBang                   '!'
        pDoubleBar              '||'
        pPlusEquals             '+='
        pMinusEquals            '-='
        pStarEquals             '*='
        pSlashEquals            '/='
        pPercentEquals          '%='
        pDoubleEquals           '=='

        lastSyntaxToken = pDoubleEquals;

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral

        sProgram
        sParmBegin
        sParmEnd
        sConst
        sVar
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sWhileStmt
        sRepeatStmt
        sRepeatEnd
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sNewLine
        sEndOfFile

        sLike
        sPackage
        sPublic
        sConcatinate
        sRepeatString
        sSubstring
        sLength
        sInitialValue
        sCaseElse

        lastSemanticToken = sCaseElse;


rules

Program :
        'using'  .sProgram

        % program parameters, needs to be at least 1
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }

        .sParmEnd ';'
        .sBegin  @Block .sEnd ;

% this becomes the body for all declarations and statements
Block :
        {[
            | 'val':
                .sConst
                @NonPublicConstantDefinitions

            | 'var':
                .sVar
                @NonPublicVariableDeclarations

            | pIdentifier:
                @AssignmentOrCallStmt

            | 'pkg':
                @PkgStatement

            | 'if':
                @IfStmt
            
            | 'choose':
                @ChooseStmt

            | 'fun' :
                .sProcedure
                @NonPublicFunction

            | 'public' :
                % can have either val, var or fun
                [
                    | 'val' :
                        .sConst
                        @PublicConstantDefinition
                    | 'var' :
                        .sVar
                        @PublicVariableDeclaration
                    | 'fun' :
                        .sProcedure
                        @PublicFunctionDeclaration
                ]
                
            | 'repeat':

                [
                    | 'while':
                        @WhileStmt
                    |*:
                        @RepeatStmt
                ]

            | *:
                >
        ]};

PublicVariableDeclaration :
        % Accept one variable declarations.
        pIdentifier  .sIdentifier
        .sPublic
        [
            | '=' :
                .sInitialValue
                @Expression .sExpnEnd
                ';'
            | * :
                @LikeClause
                ';'
        ];



PublicConstantDefinition :
        % Accept one or more named constant definitions
        pIdentifier  .sIdentifier
        .sPublic
        '=' @ConstantValue
        {[  
            | ',' :
                pIdentifier .sIdentifier .sPublic
                '=' @ConstantValue 
            | ';' :
                >
        ]};

    
PublicFunctionDeclaration :
        % procedure name
        pIdentifier  .sIdentifier
        .sPublic
        
        @ProcedureHeading
        'is' .sBegin 
        @Block 
        'end' .sEnd ';'
    ;

NonPublicFunction :
        % procedure name
        pIdentifier  .sIdentifier
        
        @ProcedureHeading
        'is' .sBegin 
        @Block 
        'end' .sEnd ';'
    ;

NonPublicConstantDefinitions :
        % Accept one or more named constant definitions
        pIdentifier  .sIdentifier
        '=' @ConstantValue
        {[  
            | ',' :
                pIdentifier .sIdentifier
                '=' @ConstantValue 
            | ';' :
                >
        ]};

ConstantValue :
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pStringLiteral:
                .sStringLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

NonPublicVariableDeclarations :
        % Accept one variable declarations.
        pIdentifier  .sIdentifier
        [
            | '=' :
                .sInitialValue
                @Expression .sExpnEnd
                ';'
            | * :
                @LikeClause
                ';'
        ];

LikeClause :
    % Check for optinal array
    [
        | '[' :
            .sArray @UnsignedIntegerConstant ']'
        | * :
    ]
    ':'

    %check for optinoal file
    [
        | 'file' :
            .sFile
        | * :
    ]
    
    % match the type
    'like' .sLike @ConstantValue
    ;


ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
        [
            | '(':
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    @LikeClause
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:
        ]
        .sParmEnd;


PkgStatement:
    .sPackage
    pIdentifier .sIdentifier
    'is'
    .sBegin
        @Block
    .sEnd
    ;

AssignmentOrCallStmt :
        [
            | '=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                @Expression
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
                ']'  '='
                @Expression
                .sExpnEnd

            % new double character assignments expand to proper postifx
            | '+=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier    
                @Expression
                .sAdd
                .sExpnEnd
            | '-=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sSubtract
                .sExpnEnd
            | '*=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sMultiply
                .sExpnEnd
            | '/=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sDivide
                .sExpnEnd
            | '%=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sModulus
                .sExpnEnd

            
            | *:
                @CallStmt
        ]
        ';' % match a semocolon after an assignment statement
        ;

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statement rule
        % accept zero or more actual parameters
        [
            | '(':
                {
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:        % no actual parameters
        ]
        .sParmEnd;


IfStmt :
%   if expression then
%       { declarationOrStatement }
%   { elseif expression then
%       { declarationOrStatement } }
%   [ else
%       { declarationOrStatement } ]
%   end ;

        .sIfStmt
        @Expression
        .sExpnEnd
        'then'  .sThen
        .sBegin @Block .sEnd

        {[ 
            %nested if else statements to implement if else 
            | 'elseif': 
                .sElse
                .sBegin
                @IfStmt
                .sEnd
            | 'else':
                .sElse
                .sBegin @Block .sEnd
            | 'end':
                >
        ]}
        ';'
        ;

ChooseStmt :
        %   choose expression of
        %   when constant { , constant } then
        %       { declarationOrStatement }
        %   { when constant { , constant } then
        %       { declarationOrStatement } }
        %   [ else
        %       { declarationOrStatement } ]
        %   end;

        .sCaseStmt
        @Expression
        .sExpnEnd
        'of' 
        %when constant { , constant } then
        'when'
        @CaseAlternative
        {[
            
                        
            | 'when':
                @CaseAlternative
            
            | *:
                >
        ]}
        .sCaseEnd
        [
            | 'else':
                .sCaseElse
                .sBegin @Block .sEnd 
            | *:
        ]
        'end' ';'
        ;

CaseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants
        
        % constant { , constant } then
        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        .sLabelEnd
        'then' 
        .sBegin @Block .sEnd;

WhileStmt :
    %   repeat while expression
    %       { declarationOrStatement }
    %   end ;

        .sWhileStmt
        @Expression
        .sExpnEnd
        .sBegin @Block 
        'end' .sEnd ';';

RepeatStmt :
    %   repeat
    %       { declarationOrStatement }
    %   while expression ;

        .sRepeatStmt
        {
            .sBegin @Block .sEnd
            [
                | 'while':
                    .sRepeatEnd
                    >
            ]
        }
        @Expression .sNot
        .sExpnEnd;

Expression :
        @SimpleExpression
        [
            | '==':
                @SimpleExpression  .sEq
            | '!=':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | '|':
                @Term .sConcatinate
            | '||':
                @Term .sRepeatString
            | *:
                >
        ]};

Term :
        @Factor
        {[
            | '*':
                @Factor  .sMultiply
            | '/':
                [
                    | pInteger: 
                    .sInteger
                        [
                            | ':':
                                @Term
                                .sSubstring
                            | *:
                                .sDivide
                        ]
                    | *:
                    @Factor  .sDivide
                ]  
            %edit within the slash for sSubstring
            | '%':
                @Factor  .sModulus
            | 'and':
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Factor :
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | 'not':
                @Factor
                .sNot
            | pStringLiteral:
                .sStringLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
            | pHash:
                @Factor
                .sLength
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
